# Week 2

This is a Julia markdown file -- an alternative to the Jupyter notebooks we have been using.

They work like R markdown documents. Write text meant for humans everywhere, except in "code blocks" which start and end with three back tick quotation marks.

These documents can be converted to many formats (html, pdf) using tools such as [Quarto](https://quarto.org/). The easiest method is to use the Julia package Weave.jl. Here's the code (not executed here) to convert this document to html:

```
using Weave # need to install it first
weave("w2-julia.jmd")
```

These files work very well in Visual Studio Code, duplicating many of the features of a Jupyter notebook in that editor.

Simple example:

```julia
1+1
```

## Linear systems

Make an interpolating polynomial.

```julia
N = 5
t = range(0, 2*pi, length = N)
y = sin.(t)
V = hcat( [t .^ i for i in 0:N-1]...) # How does this work? evaluate just the expression in the square brackets first
```

Make the Vandermonde matrix with a loop

```julia
# loop to make V
V = zeros(Float64, N, N)
for i in 1:N
    V[:, i] = t .^ (i-1)
end
# list comprehension to make a vector, then reshape to a matrix
# list comprehension to make a vector, then reshape to a matrix
V = reshape([ t[j] ^ (i-1) for i in 1:N for j in 1:N ], N, N) # fills down columns first
# double loop makes a matrix without reshaping - loop over rows first
V = [ t[j] ^ (i-1) for j in 1:N, i in 1:N ]
```

Get the coefficients, make a polynomial.

```julia
c = V\y # coefficients
using Polynomials
p = Polynomial(c)
```

Plot the result.

```julia
using Plots
plot(p, 0, 2*pi, label = "interpolation")
scatter!(t, y, label = "Points")
plot!(sin, 0, 2*pi, label = "Sin")
```

## Forward and backward substitution

From textbook, section 2.3.

```julia
"""
    forwardsub(L,b)

Solve the lower triangular linear system with matrix `L` and
right-hand side vector `b`.
"""
function forwardsub(L,b)
    n = size(L,1)
    x = zeros(n)
    x[1] = b[1]/L[1,1]
    for i in 2:n
        s = sum( L[i,j]*x[j] for j in 1:i-1 )
        x[i] = ( b[i] - s ) / L[i,i]
    end
    return x
end

"""
    backsub(U,b)

Solve the upper triangular linear system with matrix `U` and
right-hand side vector `b`.
"""
function backsub(U,b)
    n = size(U,1)
    x = zeros(n)
    x[n] = b[n]/U[n,n]
    for i in n-1:-1:1
        s = sum( U[i,j]*x[j] for j in i+1:n )
        x[i] = ( b[i] - s ) / U[i,i]
    end
    return x
end
```

Generate an upper triangular and a lower triangular matrix. Make A from the product. 

```julia
using LinearAlgebra
L = LowerTriangular(rand(Float64, 3, 3)) # compare tril(rand(Float64, 3, 3))
U = UpperTriangular(rand(Float64, 3, 3)) # compare triu(rand(Float64, 3, 3))
L[diagind(L)] = ones(3) # Make L have 1s on the diagonal
b = [1, 2, 3];
A = L * U; # Matrix product
```

Solve $Ax = b$ by writing $A = LU$ and solving $LUx = b$. 
Solve this in two steps: $Lz = B$ then $Ux = z$.
Compare with solution to $Ax = b$.

```julia
z = forwardsub(L, b)
L\b # check with Julia's built-in solve
x = backsub(U, z)
U\z # check
A\b
L
U
sizeof(A), sizeof(L), sizeof(U)
F = lu(A)
F.p # see also F.P, F.L, F.U
```

## Inner and outer products of vectors

```julia
x = [1, 5, 9, -1]
y = [-1 3 0 2]
x * y # outer product
y * x # inner (dot) product
x'x
y * y'
```

## Norms and matrix condition number

```julia
norm(b)
sqrt(b'b)
b
norm(b, 1)
norm(b, Inf)
norm(A)
sqrt(sum(A .^ 2))
norm(A, Inf)
maximum(A)
norm(A,1)
sum(abs.(A))
cond(A)
```

Hilbert matrix

```julia
D = [ 1/(1+i+j) for i in 1:9, j in 1:9 ] # Hilbert matrix
# D = reshape([ 1/(1+i+j) for i in 1:9 for j in 1:9 ], 9, 9)
cond(D,1), cond(D,2), cond(D, Inf)
 = 1:9
b = D*x  
@show D\b;
Δx = D\b - x
@show relative_error = norm(Δx) / norm(x);
```

Symmetrix matrix

```julia
S = A .+ A'
isposdef(S) # possible, but not guaranteed
SPD = A' * A
C = cholesky(SPD)
C.L
C.L * C.L'
```

## Summary

* create column, row vectors
* create matrix by combining vectors, from a function and pair of loops
* zeros, ones
* diagm, tril, triu, UpperTriangular, LowerTriangular
* rand (scalar, vector, matrix)
* matrix, vector multiply
* transpose ' (adjoint = complex conjugate transpose)
* solve \
* scalar multiply (broadcasting with .)
* subsetting (indexes, :)

